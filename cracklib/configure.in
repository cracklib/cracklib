dnl Process this file with autoconf to produce a configure script.
AC_INIT([lib/crack.h])
AM_INIT_AUTOMAKE(cracklib, 2.8.4)
AM_CONFIG_HEADER(config.h)

dnl Set of available languages.
ALL_LINGUAS="cs de es fr hu it nl sk"

dnl Checks for programs.
AC_PROG_LIBTOOL
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

AC_PATH_XTRA

AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADERS(fcntl.h)
AC_CHECK_HEADERS(inttypes.h)
AC_CHECK_HEADERS(stdint.h)
AC_CHECK_HEADERS(pthread.h)

dnl Cygwin workaround
AC_MSG_CHECKING(if LINE_MAX is defined)
AC_EGREP_CPP(yes,
[#include <limits.h>
#ifdef LINE_MAX
yes
#endif
], line_max_defined=yes, line_max_defined=no)
AC_MSG_RESULT($line_max_defined)

if test "$line_max_defined" = "no"; then
   CFLAGS="$CFLAGS -DLINE_MAX=2048"
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Check for utility functions that may need to be replaced
AC_CHECK_FUNCS(strdup)
AC_CHECK_FUNCS(getpwuid_r)

dnl internationalization macros
AM_GNU_GETTEXT_VERSION
AM_GNU_GETTEXT([external])

dnl Check for python, unless we were told to not try to build a python module
AC_ARG_WITH(python,
AC_HELP_STRING(--without,[Build a python module (default auto)]),
build_python=$withval,build_python=auto)
if test "$build_python" != no ; then
   AM_PATH_PYTHON(,,[
   if test "$build_python" = auto ; then
      AC_MSG_WARN([python was not found, continuing])
      build_python=no
   else
      AC_MSG_ERROR([python was required but not found])
   fi
   ])
   if test "$build_python" != no ; then
      build_python=yes
   fi
fi
AM_CONDITIONAL(BUILD_PYTHON,[test "$build_python" = "yes"])

AC_OUTPUT(util/Makefile lib/Makefile doc/Makefile python/Makefile Makefile \
		po/Makefile.in m4/Makefile cracklib.spec)

